import org.gradle.api.publish.maven.internal.artifact.FileBasedMavenArtifact

ext {
    bintrayRepo = "chrynan"
    libraryGroup = "com.chrynan.aaaah"
    libraryOwner = "chrynan"
    libraryRepo = "aaaah"
    libraryVersion = "0.7.0"
    libraryVersionCode = 2
    libraryVersionDescription = "Second Major Release"
    githubURL = "https://github.com/chRyNaN/aaaah.git"
}

group libraryGroup
version libraryVersion

buildscript {
    repositories {
        mavenCentral()
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:3.6.2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.71"
        classpath "digital.wup:android-maven-publish:3.6.2"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    apply plugin: "maven"
    apply plugin: "maven-publish"
    apply plugin: "com.jfrog.bintray"

    bintray {
        user = project.findProperty("BINTRAY_USER") ?: System.getenv("BINTRAY_USER")
        key = project.findProperty("BINTRAY_KEY") ?: System.getenv("BINTRAY_KEY")
        pkg {
            repo = rootProject.bintrayRepo
            name = rootProject.libraryRepo
            licenses = ["Apache-2.0"]
            vcsUrl = rootProject.githubURL
            version {
                name = rootProject.libraryVersion
                desc = rootProject.libraryVersionDescription
                released = new Date()
                vcsTag = rootProject.libraryVersion
            }
        }
    }

    bintrayUpload.doFirst {
        publications = publishing.publications.collect {
            File moduleFile = project.buildDir.toPath()
                    .resolve("publications/${it.name}/module.json").toFile()

            if (moduleFile.exists()) {
                it.artifact(new FileBasedMavenArtifact(moduleFile) {
                    @Override
                    protected String getDefaultExtension() {
                        return "module"
                    }
                })
            }

            it.name
        }
    }

    bintrayUpload.dependsOn publishToMavenLocal
}